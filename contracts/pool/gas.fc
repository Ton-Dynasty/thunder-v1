const gas::gas_consumption = 20000000; ;; 0.2 gas
const gas::forward_ton = 100000000; ;; 0.1 gas

const MIN_STORAGE_DURATION = 72 * 3600; ;; 3 days
const POOL_DEPOSIT_GAS = 12495;

const POOL_BITS = 14268;
const POOL_CELLS = 28;

;; Jetton transfer fees
const JETTON_NOTIFY_GAS_CONSUMPTION = 16721;
const SEND_TRANSFER_GAS_CONSUMPTION    = 9255;
const RECEIVE_TRANSFER_GAS_CONSUMPTION = 10355;

const JETTON_WALLET_INITSTATE_BITS  = 931;
const JETTON_WALLET_INITSTATE_CELLS = 3;

int calculate_pool_min_storage_fee() inline {
    return get_storage_fee(MY_WORKCHAIN, MIN_STORAGE_DURATION, POOL_BITS, POOL_CELLS);
}

int forward_wallet_init_state_overhead() inline {
    return get_simple_forward_fee(MY_WORKCHAIN, JETTON_WALLET_INITSTATE_BITS, JETTON_WALLET_INITSTATE_CELLS);
}

int get_jetton_transfer_fee() inline {
    int pool_gas_consumption = get_precompiled_gas_consumption();
    int send_transfer_gas_consumption = null?(pool_gas_consumption) ? SEND_TRANSFER_GAS_CONSUMPTION : pool_gas_consumption;
    int receive_transfer_gas_consumption = null?(pool_gas_consumption) ? RECEIVE_TRANSFER_GAS_CONSUMPTION : pool_gas_consumption;
    return get_compute_fee(MY_WORKCHAIN, send_transfer_gas_consumption) + get_compute_fee(MY_WORKCHAIN, receive_transfer_gas_consumption);
}

(int, int) cal_deposit_asset_fee(int fwd_fee) inline {
    int pool_gas_consumption = get_precompiled_gas_consumption();
    int pool_deposit_consumption = null?(pool_gas_consumption) ? POOL_DEPOSIT_GAS : pool_gas_consumption;    

    int gas_comp = get_compute_fee(MY_WORKCHAIN, pool_deposit_consumption) + 
    calculate_pool_min_storage_fee() + 
    forward_wallet_init_state_overhead();
    
    int total_fee = gas_comp + 
    fwd_fee * 2  + 
    get_jetton_transfer_fee();

    return (total_fee, gas_comp);
}