#include "../common/gas.fc";

int calculate_dex_router_min_storage_fee() inline {
    return get_storage_fee(MY_WORKCHAIN, MIN_STORAGE_DURATION, DEX_ROUTER_BITS, DEX_ROUTER_CELLS);
}

int forward_init_state_overhead() inline {
    return get_simple_forward_fee(MY_WORKCHAIN, POOL_BITS, POOL_CELLS);
}

int get_jetton_transfer_fee() inline {
    int pool_gas_consumption = get_precompiled_gas_consumption();
    int send_transfer_gas_consumption = null?(pool_gas_consumption) ? SEND_TRANSFER_GAS_CONSUMPTION : pool_gas_consumption;
    int receive_transfer_gas_consumption = null?(pool_gas_consumption) ? RECEIVE_TRANSFER_GAS_CONSUMPTION : pool_gas_consumption;
    return get_compute_fee(MY_WORKCHAIN, send_transfer_gas_consumption) + get_compute_fee(MY_WORKCHAIN, receive_transfer_gas_consumption);
}

(int, int) cal_deploy_pool_fee(int fwd_fee) inline {
    int pool_gas_consumption = get_precompiled_gas_consumption();
    int send_to_dex_router_consumption = null?(pool_gas_consumption) ? SEND_TO_DEX_ROUTER : pool_gas_consumption;

    int deploy_pool_gas_consumption = null?(pool_gas_consumption) ? DEPLOY_POOL_GAS : pool_gas_consumption;
    int deposit_asset_gas_consumption = null?(pool_gas_consumption) ? POOL_DEPOSIT_GAS : pool_gas_consumption;

    int gas_comp = get_compute_fee(MY_WORKCHAIN, send_to_dex_router_consumption) + 
    calculate_dex_router_min_storage_fee();
    
    int total_fee = gas_comp + 
    get_jetton_transfer_fee() + 
    get_compute_fee(MY_WORKCHAIN, deposit_asset_gas_consumption) +
    get_compute_fee(MY_WORKCHAIN, deploy_pool_gas_consumption) + 
    fwd_fee * 3;

    return (total_fee, gas_comp);
}

(int, int) cal_swap_fee(int fwd_fee) inline {
    int pool_gas_consumption = get_precompiled_gas_consumption();
    int router_swap_internal_consumption = null?(pool_gas_consumption) ? SWAP_INTERNAL_DR_GAS : pool_gas_consumption;
    int pool_swap_internal_consumption = null?(pool_gas_consumption) ? SWAP_INTERNAL_POOL_GAS : pool_gas_consumption;

    int gas_comp = get_compute_fee(MY_WORKCHAIN, router_swap_internal_consumption) + 
    calculate_dex_router_min_storage_fee();
    
    int total_fee = gas_comp + 
    get_jetton_transfer_fee() + 
    get_compute_fee(MY_WORKCHAIN, pool_swap_internal_consumption) +
    fwd_fee * 5;

    return (total_fee, gas_comp);
}

(int, int) cal_packout_fee(int fwd_fee) inline {
    int pool_gas_consumption = get_precompiled_gas_consumption();
    int router_packout_gas = null?(pool_gas_consumption) ? PACKOUT_FROM_POOL_GAS : pool_gas_consumption;
    int pool_swap_internal_consumption = null?(pool_gas_consumption) ? SWAP_INTERNAL_POOL_GAS : pool_gas_consumption;

    int gas_comp = get_compute_fee(MY_WORKCHAIN, router_packout_gas) + 
    calculate_dex_router_min_storage_fee();
    
    int total_fee = gas_comp + 
    get_jetton_transfer_fee() + 
    fwd_fee * 3;

    return (total_fee, gas_comp);
}